buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.mod_version}"
group= "com.dyonovan.brlib" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BR-Lib"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = config.minecraft_version + "-" + config.forge_version + "-" + config.minecraft_version

    replaceIn "Constants.java"
    replace "@VERSION@", "${config.mod_version}"

    runDir = "eclipse"
}

dependencies {

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }
    extension = 'jar'
    classifier = 'dev'
}

artifacts {
    archives devJar
}

configurations {
    deployerJars
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: System.properties['URL']) {
                authentication(userName: System.properties['HTTPUSERNAME'], password: System.properties['HTTPPASSWORD'])
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName

                pom.project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'BRLib'
                    url 'https://github.com/TeamBR-Modding/TeamBR-Core/'
                    scm {
                        url 'https://github.com/TeamBR-Modding/TeamBR-Core/'
                        connection 'scm:git:git://github.com/TeamBR-Modding/TeamBR-Core.git'
                        developerConnection 'scm:git:git@github.com/TeamBR-Modding/TeamBR-Core.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/TeamBR-Modding/TeamBR-Core/issues'
                    }
                    licenses {
                        license {
                            name 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International License'
                            url 'http://creativecommons.org/licenses/by-nc-sa/4.0/'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Dyonovan'
                            name 'Dyonovan'
                            roles {
                                role 'developer'
                            }
                        }
                        developer {
                            id 'pauljoda'
                            name 'pauljoda'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}

